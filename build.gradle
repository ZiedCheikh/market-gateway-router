plugins {
	id 'java'
	alias(libs.plugins.spring.boot)
	alias(libs.plugins.spring.dependency)
	id 'checkstyle'
	id 'pmd'
	alias(libs.plugins.spotbugs)
	alias(libs.plugins.dependency.updates )
}
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

wrapper {
	gradleVersion = libs.versions.gradle.get()
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${libs.versions.spring.cloud.get()}"
    }
}

dependencies {
	implementation libs.spring.oauth2.client
	implementation libs.spring.security
	implementation libs.spring.webflux
	implementation libs.spring.gateway
	implementation libs.spring.eureka.client
	implementation libs.spring.actuator
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = true
		xml.outputLocation = rootProject.file("build/reports/checkstyle/checkstyle.xml")
	}
}

pmd {
	toolVersion = libs.versions.pmd.get()
	ruleSetFiles = files("config/pmd/pmd.xml")
	ruleSets = []
}

tasks.withType(Pmd) {
	reports {
		xml.required = true
		xml.outputLocation = rootProject.file("build/reports/pmd/pmd.xml")
		html.required = true
		html.outputLocation = rootProject.file("build/reports/pmd/pmd.html")
	}
	setSource(fileTree(dir: "${projectDir}/src/", include: "**/*.java"))
}

spotbugs {
	ignoreFailures = true
	showStackTraces = false
}